module CoercionExpr.CoercionExpr where

open import Data.Nat
open import Data.Unit using (⊤; tt)
open import Data.Bool using (true; false) renaming (Bool to 𝔹)
open import Data.List hiding ([_])
open import Data.Product renaming (_,_ to ⟨_,_⟩)
open import Data.Sum using (_⊎_)
open import Data.Maybe
open import Relation.Nullary using (¬_; Dec; yes; no)
open import Relation.Binary.PropositionalEquality using (_≡_; refl)
open import Function using (case_of_)

open import Common.Utils
open import Common.SecurityLabels
open import Common.BlameLabels
open import CoercionExpr.Coercions public


infixl 8 _⨾_  {- syntactic composition -}

data CExpr_⇒_ : Label → Label → Set where

  id : ∀ g → CExpr g ⇒ g

  _⨾_ : ∀ {g₁ g₂ g₃} → CExpr g₁ ⇒ g₂ → ⊢ g₂ ⇒ g₃ → CExpr g₁ ⇒ g₃

  ⊥ : ∀ g₁ g₂ (p : BlameLabel) → CExpr g₁ ⇒ g₂


coerce-to⋆ : ∀ g → CExpr g ⇒ ⋆
coerce-to⋆    ⋆  = id ⋆
coerce-to⋆ (l ℓ) = id (l ℓ) ⨾ ℓ !

coerceₗ : ∀ {g₁ g₂} → g₁ ≾ g₂ → BlameLabel → CExpr g₁ ⇒ g₂
coerceₗ {g}      {⋆}      _         _ = coerce-to⋆ g
coerceₗ {⋆}      {l ℓ}    ≾-⋆l      p = id ⋆ ⨾ ℓ ?? p
coerceₗ {l low}  {l low}  (≾-l l≼l) _ = id (l low)
coerceₗ {l low}  {l high} (≾-l l≼h) _ = id (l low) ⨾ ↑
coerceₗ {l high} {l high} (≾-l h≼h) _ = id (l high)


-- data CVal : ∀ {g₁ g₂} → CExpr g₁ ⇒ g₂ → Set where

--   id : ∀ {g} → CVal (id g)

--   up : CVal ((id (l low)) ⨾ ↑)

--   inj : ∀ {ℓ} → CVal ((id (l ℓ)) ⨾ (ℓ !))

--   proj : ∀ {ℓ p} → CVal ((id ⋆) ⨾ (ℓ ?? p))

--   up-inj : CVal ((id (l low)) ⨾ ↑ ⨾ (high !))

--   proj-up : ∀ {p} → CVal ((id ⋆) ⨾ (low ?? p) ⨾ ↑)

--   proj-inj : ∀ {ℓ p} → CVal ((id ⋆) ⨾ (ℓ ?? p) ⨾ (ℓ !))

--   proj-up-inj : ∀ {p} → CVal ((id ⋆) ⨾ (low ?? p) ⨾ ↑ ⨾ (high !))

data CVal : ∀ {g₁ g₂} → CExpr g₁ ⇒ g₂ → Set where

  id : ∀ {g} → CVal (id g)

  id⨾? : ∀ {ℓ p} → CVal ((id ⋆) ⨾ (ℓ ?? p))

  inj : ∀ {g ℓ} {c̅ : CExpr g ⇒ l ℓ} → CVal c̅ → CVal (c̅ ⨾ (ℓ !))

  up : ∀ {g} {c̅ : CExpr g ⇒ l low} → CVal c̅ → CVal (c̅ ⨾ ↑)


infix 2 _—→_

data _—→_ : ∀ {g₁ g₂} → CExpr g₁ ⇒ g₂ → CExpr g₁ ⇒ g₂ → Set where

  ξ : ∀ {g₁ g₂ g₃} {c̅ c̅′ : CExpr g₁ ⇒ g₂} {c : ⊢ g₂ ⇒ g₃}
    → c̅  —→ c̅′
      ----------------------
    → c̅ ⨾ c  —→ c̅′ ⨾ c

  ξ-⊥ : ∀ {p} {g₁ g₂ g₃} {c : ⊢ g₂ ⇒ g₃}
      ------------------------------------------
    → (⊥ g₁ g₂ p) ⨾ c  —→ ⊥ g₁ g₃ p

  id : ∀ {g₁ g₂} {c̅ : CExpr g₁ ⇒ g₂}
    → CVal c̅
      --------------------------
    → c̅ ⨾ (id g₂)  —→ c̅

  ?-id : ∀ {p} {g ℓ} {c̅ : CExpr g ⇒ (l ℓ)}
    → CVal c̅
      ----------------------------------
    → c̅ ⨾ (ℓ !) ⨾ (ℓ ?? p)  —→ c̅

  ?-↑ : ∀ {p} {g} {c̅ : CExpr g ⇒ (l low)}
    → CVal c̅
      ---------------------------------------
    → c̅ ⨾ (low !) ⨾ (high ?? p)  —→ c̅ ⨾ ↑

  ?-⊥ : ∀ {p} {g} {c̅ : CExpr g ⇒ (l high)}
    → CVal c̅
      -----------------------------------------------
    → c̅ ⨾ (high !) ⨾ (low ?? p)  —→ ⊥ g (l low) p

infix  2 _—↠_
infixr 2 _—→⟨_⟩_
infix  3 _∎

data _—↠_ : ∀ {g₁ g₂} (c̅₁ c̅₂ : CExpr g₁ ⇒ g₂) → Set where
  _∎ : ∀ {g₁ g₂} (c̅ : CExpr g₁ ⇒ g₂)
      ---------------
    → c̅ —↠ c̅

  _—→⟨_⟩_ : ∀ {g₁ g₂} (c̅₁ : CExpr g₁ ⇒ g₂) {c̅₂ c̅₃}
    → c̅₁ —→ c̅₂
    → c̅₂ —↠ c̅₃
      ---------------
    → c̅₁ —↠ c̅₃

plug-cong : ∀ {g₁ g₂ g₃} {M N : CExpr g₁ ⇒ g₂} {c : ⊢ g₂ ⇒ g₃}
  → M —↠ N
  → M ⨾ c —↠ N ⨾ c
plug-cong (M ∎) = (M ⨾ _) ∎
plug-cong (M —→⟨ M→L ⟩ L↠N) = M ⨾ _ —→⟨ ξ M→L ⟩ (plug-cong L↠N)

↠-trans : ∀ {g₁ g₂} {L M N : CExpr g₁ ⇒ g₂}
  → L —↠ M
  → M —↠ N
  → L —↠ N
↠-trans (L ∎) (._ ∎) = L ∎
↠-trans (L ∎) (.L —→⟨ M→ ⟩ ↠N) = L —→⟨ M→ ⟩ ↠N
↠-trans (L —→⟨ L→ ⟩ ↠M) (M ∎) = L —→⟨ L→ ⟩ ↠M
↠-trans (L —→⟨ L→ ⟩ ↠M) (M —→⟨ M→ ⟩ ↠N) = L —→⟨ L→ ⟩ ↠-trans ↠M (M —→⟨ M→ ⟩ ↠N)


data Progress : ∀ {g₁ g₂} → (c̅ : CExpr g₁ ⇒ g₂) → Set where

  done : ∀ {g₁ g₂} {c̅ : CExpr g₁ ⇒ g₂}
    → CVal c̅
    → Progress c̅

  error : ∀ {p} {g₁ g₂} → Progress (⊥ g₁ g₂ p)

  step : ∀ {g₁ g₂} {c̅ c̅′ : CExpr g₁ ⇒ g₂}
    → c̅  —→ c̅′
    → Progress c̅


progress : ∀ {g₁ g₂} (c̅ : CExpr g₁ ⇒ g₂) → Progress c̅
progress (id g) = done id
progress (c̅ ⨾ c) with progress c̅
... | step c̅→c̅′ = step (ξ c̅→c̅′)
... | error = step ξ-⊥
... | done id with c
progress (_ ⨾ c) | done id | id g   = step (id id)
progress (_ ⨾ c) | done id | ↑     = done (up id)
progress (_ ⨾ c) | done id | ℓ !    = done (inj id)
progress (_ ⨾ c) | done id | ℓ ?? p = done id⨾?
progress (_ ⨾ c) | done id⨾? with c
progress (_ ⨾ c) | done id⨾? | id _ = step (id id⨾?)
progress (_ ⨾ c) | done id⨾? | ↑   = done (up id⨾?)
progress (_ ⨾ c) | done id⨾? | ℓ₁ ! = done (inj id⨾?)
progress (_ ⨾ c) | done (inj v) with c
progress (_ ⨾ c) | done (inj v) | id ⋆ = step (id (inj v))
progress (_ ⨾ c) | done (inj {ℓ = low}  v) | low  ?? p = step (?-id v)
progress (_ ⨾ c) | done (inj {ℓ = high} v) | high ?? p = step (?-id v)
progress (_ ⨾ c) | done (inj {ℓ = low}  v) | high ?? p = step (?-↑ v)
progress (_ ⨾ c) | done (inj {ℓ = high} v) | low  ?? p = step (?-⊥ v)
progress (_ ⨾ c) | done (up v) with c
progress (_ ⨾ c) | done (up v) | id (l high) = step (id (up v))
progress (_ ⨾ c) | done (up v) | high !      = done (inj (up v))
progress (⊥ g₁ g₂ p) = error



-- data Result : ∀ {g₁ g₂} → (c̅ : CExpr g₁ ⇒ g₂) → Set where

--   success : ∀ {g₁ g₂} {c̅ c̅′ : CExpr g₁ ⇒ g₂}
--     → c̅ —↠ c̅′
--     → CVal c̅′
--     → Result c̅

--   fail : ∀ {g₁ g₂} {c̅ : CExpr g₁ ⇒ g₂} {p}
--     → c̅ —↠ ⊥ g₁ g₂ p
--     → Result c̅

data CResult : ∀ {g₁ g₂} → (c̅ : CExpr g₁ ⇒ g₂) → Set where

  success : ∀ {g₁ g₂} {c̅ : CExpr g₁ ⇒ g₂} → CVal c̅ → CResult c̅

  fail    : ∀ {g₁ g₂ p} → CResult (⊥ g₁ g₂ p)


_⇓_ : ∀ {g₁ g₂} → (c̅ d̅ : CExpr g₁ ⇒ g₂) → Set
c̅ ⇓ d̅ = (c̅ —↠ d̅) × CResult d̅


result : ∀ {g₁ g₂} (c̅ : CExpr g₁ ⇒ g₂) → ∃[ d̅ ] c̅ ⇓ d̅
result (id g) = ⟨ _ , _ ∎ , success id ⟩
result (⊥ g₁ g₂ p) = ⟨ _ , _ ∎ , fail ⟩
result (c̅ ⨾ c)
  with result c̅
... | ⟨ _ , c̅′↠c̅″ , success id ⟩
  with c
result (_ ⨾ c) | ⟨ _ , c̅′↠c̅″ , success id ⟩ | id g   =
  ⟨ _ , ↠-trans (plug-cong c̅′↠c̅″) (_ —→⟨ id id ⟩ _ ∎) , success id ⟩
result (_ ⨾ c) | ⟨ _ , c̅′↠c̅″ , success id ⟩ | ↑     =
  ⟨ _ , plug-cong c̅′↠c̅″ , success (up id) ⟩
result (_ ⨾ c) | ⟨ _ , c̅′↠c̅″ , success id ⟩ | ℓ !    =
  ⟨ _ , plug-cong c̅′↠c̅″ , success (inj id) ⟩
result (_ ⨾ c) | ⟨ _ , c̅′↠c̅″ , success id ⟩ | ℓ ?? p =
  ⟨ _ , plug-cong c̅′↠c̅″ , success id⨾? ⟩
result (_ ⨾ c) | ⟨ _ , c̅′↠c̅″ , success id⨾? ⟩
  with c
result (_ ⨾ c) | ⟨ _ , c̅′↠c̅″ , success id⨾? ⟩ | id _ =
  ⟨ _ , ↠-trans (plug-cong c̅′↠c̅″) (_ —→⟨ id id⨾? ⟩ _ ∎) , success id⨾? ⟩
result (_ ⨾ c) | ⟨ _ , c̅′↠c̅″ , success id⨾? ⟩ | ↑   =
  ⟨ _ , plug-cong c̅′↠c̅″ , success (up id⨾?) ⟩
result (_ ⨾ c) | ⟨ _ , c̅′↠c̅″ , success id⨾? ⟩ | ℓ₁ ! =
  ⟨ _ , plug-cong c̅′↠c̅″ , success (inj id⨾?) ⟩
result (_ ⨾ c) | ⟨ _ , c̅′↠c̅″ , success (inj v) ⟩
  with c
result (_ ⨾ c) | ⟨ _ , c̅′↠c̅″ , success (inj v) ⟩ | id ⋆ =
  ⟨ _ , ↠-trans (plug-cong c̅′↠c̅″) (_ —→⟨ id (inj v) ⟩ _ ∎) , success (inj v) ⟩
result (_ ⨾ c) | ⟨ _ , c̅′↠c̅″ , success (inj {ℓ = low}  v) ⟩ | low  ?? p =
  ⟨ _ , ↠-trans (plug-cong c̅′↠c̅″) (_ —→⟨ ?-id v ⟩ _ ∎) , success v ⟩
result (_ ⨾ c) | ⟨ _ , c̅′↠c̅″ , success (inj {ℓ = high} v) ⟩ | high ?? p =
  ⟨ _ , ↠-trans (plug-cong c̅′↠c̅″) (_ —→⟨ ?-id v ⟩ _ ∎) , success v ⟩
result (_ ⨾ c) | ⟨ _ , c̅′↠c̅″ , success (inj {ℓ = low}  v) ⟩ | high ?? p =
  ⟨ _ , ↠-trans (plug-cong c̅′↠c̅″) (_ —→⟨ ?-↑ v ⟩ _ ∎) , success (up v) ⟩
result (_ ⨾ c) | ⟨ _ , c̅′↠c̅″ , success (inj {ℓ = high} v) ⟩ | low  ?? p =
  ⟨ _ , ↠-trans (plug-cong c̅′↠c̅″) (_ —→⟨ ?-⊥ v ⟩ _ ∎) , fail ⟩
result (_ ⨾ c) | ⟨ _ , c̅′↠c̅″ , success (up v) ⟩
  with c
result (_ ⨾ c) | ⟨ _ , c̅′↠c̅″ , success (up v) ⟩ | id (l high) =
  ⟨ _ , ↠-trans (plug-cong c̅′↠c̅″) (_ —→⟨ id (up v) ⟩ _ ∎) , success (up v) ⟩
result (_ ⨾ c) | ⟨ _ , c̅′↠c̅″ , success (up v) ⟩ | high !      =
  ⟨ _ , plug-cong c̅′↠c̅″ , success (inj (up v)) ⟩
result (_ ⨾ c) | ⟨ _ , c̅′↠⊥ , fail ⟩ =
  ⟨ _ , ↠-trans (plug-cong c̅′↠⊥) (_ —→⟨ ξ-⊥ ⟩ _ ∎) , fail ⟩
