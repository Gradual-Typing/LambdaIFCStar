module LabelCoercionCalculi.C where

open import Data.Nat
open import Data.Unit using (âŠ¤; tt)
open import Data.Bool using (true; false) renaming (Bool to ğ”¹)
open import Data.List hiding ([_])
open import Data.Product renaming (_,_ to âŸ¨_,_âŸ©)
open import Data.Sum using (_âŠ_)
open import Data.Maybe
open import Relation.Nullary using (Â¬_; Dec; yes; no)
open import Relation.Binary.PropositionalEquality using (_â‰¡_; refl)
open import Function using (case_of_)

open import Common.Utils
open import Common.SecurityLabels
open import Common.BlameLabels
open import LabelCoercionCalculi.Coercion

data LabelExp : Label â†’ Set where

  l : (â„“ : StaticLabel) â†’ LabelExp (l â„“)

  _âŸ¨_âŸ© : âˆ€ {gâ‚ gâ‚‚} â†’ LabelExp gâ‚ â†’ âŠ¢ gâ‚ â‡’ gâ‚‚ â†’ LabelExp gâ‚‚

  bl : âˆ€ {g} (p : BlameLabel) â†’ LabelExp g

data LabelVal : âˆ€ {g} â†’ LabelExp g â†’ Set where

  v-l : âˆ€ {â„“} â†’ LabelVal (l â„“)

  v-! : âˆ€ {â„“} â†’ LabelVal (l â„“ âŸ¨ â„“ ! âŸ©)

infix 2 _âŸ¶_

data _âŸ¶_ : âˆ€ {g} â†’ LabelExp g â†’ LabelExp g â†’ Set where

  Î¾ : âˆ€ {gâ‚ gâ‚‚} {e eâ€² : LabelExp gâ‚} {c : âŠ¢ gâ‚ â‡’ gâ‚‚}
    â†’ e âŸ¶ eâ€²
      ----------------------
    â†’ e âŸ¨ c âŸ© âŸ¶ eâ€² âŸ¨ c âŸ©

  Î¾-bl : âˆ€ {p} {gâ‚ gâ‚‚} {c : âŠ¢ gâ‚ â‡’ gâ‚‚} â†’ (bl p) âŸ¨ c âŸ© âŸ¶ bl p

  id : âˆ€ {g} {e : LabelExp g}
    â†’ LabelVal e
      --------------------------
    â†’ e âŸ¨ id g âŸ© âŸ¶ e

  â†‘ : l low âŸ¨ â†‘ âŸ© âŸ¶ l high

  seq : âˆ€ {gâ‚ gâ‚‚ gâ‚ƒ} {e : LabelExp gâ‚} {c : âŠ¢ gâ‚ â‡’ gâ‚‚} {d : âŠ¢ gâ‚‚ â‡’ gâ‚ƒ}
    â†’ LabelVal e
      -----------------------------------
    â†’ e âŸ¨ seq c d âŸ© âŸ¶ (e âŸ¨ c âŸ©) âŸ¨ d âŸ©

  ?-id : âˆ€ {p} {â„“} {e : LabelExp (l â„“)}
    â†’ LabelVal e
      ----------------------------------
    â†’ (e âŸ¨ â„“ ! âŸ©) âŸ¨ â„“ ?? p âŸ© âŸ¶ l â„“

  ?-â†‘ : âˆ€ {p} {e}
    â†’ LabelVal e
      ---------------------------------------
    â†’ (e âŸ¨ low ! âŸ©) âŸ¨ high ?? p âŸ© âŸ¶ l high

  ?-bl : âˆ€ {p} {e}
    â†’ LabelVal e
      ---------------------------------------
    â†’ (e âŸ¨ high ! âŸ©) âŸ¨ low ?? p âŸ© âŸ¶ bl p

  bl : âˆ€ {p} {gâ‚ gâ‚‚} {e : LabelExp gâ‚}
    â†’ LabelVal e
      -------------------------
    â†’ e âŸ¨ âŠ¥_ {gâ‚} {gâ‚‚} p âŸ© âŸ¶ bl {gâ‚‚} p

data Progress : âˆ€ {g} â†’ (e : LabelExp g) â†’ Set where

  done : âˆ€ {g} {e : LabelExp g}
    â†’ LabelVal e
    â†’ Progress {g} e

  blame : âˆ€ {p} {g} â†’ Progress (bl {g} p)

  step : âˆ€ {g} {e eâ€² : LabelExp g}
    â†’ e âŸ¶ eâ€²
    â†’ Progress e

progress : âˆ€ {g} (e : LabelExp g) â†’ Progress e
progress {l â„“} (l â„“) = done v-l
progress {g} (e âŸ¨ c âŸ©) with progress e
... | step eâ†’eâ€² = step (Î¾ eâ†’eâ€²)
... | blame = step Î¾-bl
... | done v-l with c
...   | id _ = step (id v-l)
...   | â„“ ! = done v-!
...   | â†‘ = step â†‘
...   | seq c d = step (seq v-l)
...   | âŠ¥ p = step (bl v-l)
progress {g} (e âŸ¨ c âŸ©) | done (v-! {â„“â‚}) with c
... | id _ = step (id v-!)
... | seq c d = step (seq v-!)
... | âŠ¥ p = step (bl v-!)
... | â„“â‚‚ ?? p with â„“â‚ | â„“â‚‚
...   | low | low = step (?-id v-l)
...   | high | high = step (?-id v-l)
...   | low | high = step (?-â†‘ v-l)
...   | high | low = step (?-bl v-l)
progress {g} (bl p) = blame
